#DEPENDENCIES
#sample = glob_wildcards('../DatasetCreation/SRAfiles/fastq/{folder}/{sample}.sra_1.fastq')[0]
sample = ['SRR15979873','SRR15979857','SRR15979850']
rule all:
	input: expand('../minimap_out/{sample}/{sample}_sr1.flagstats', sample = sample)
#	input: expand('../ToRemove/{sample}_ToRemove.fasta', sample = sample)


# TRIMMOMATIC

'''
Trimmomatic is a tool for read trimming in Illumina NGS generated data.
Rule one uses this bioinformatic tool in order to trim the reads of the SRR files downloaded.
Command line usage:
 - PE (paired-end data)
 - Adapters path
 - LEADING -> remove leading low quality or N bases (below quality 20)
 - TRAILING -> remove trailing low quality or N bases (below quality 20)
 - SLIDINGWINDOW -> scan the read with a 5-base wide sliding window, cut when the average quality per base drops below 20
 - MINLEN -> drop reads below the 95 bases long
'''

rule trimmomatic:
	input:
		Reads1 = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.sra_1.fastq',
		Reads2 = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.sra_2.fastq'
	output:
		l_P = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.trimmed_1P.fq',
		r_P = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.trimmed_2P.fq',
		l_U = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.trimmed_1U.fq',
		r_U = '../DatasetCreation/SRAfiles/fastq/{sample}/{sample}.trimmed_2U.fq'
	threads: 2
#	conda: '../../miniconda3/envs/snakemake'
	shell:
		'trimmomatic PE {input.Reads1} {input.Reads2} {output.l_P} {output.l_U} {output.r_P} {output.r_U} -threads {threads} '
		'ILLUMINACLIP:../../miniconda3/envs/snakemake/share/trimmomatic/adapters/TruSeq3-PE.fa:2:30:10:2:TRUE '
		'LEADING:20 TRAILING:20 SLIDINGWINDOW:5:20 MINLEN:95'

# SPADES

'''
SPAdes is a genome assembler tool.
It takes as input the two files of paired end reads. 
Using k-mers it outputs contigs.fasta and scaffold.fasta.
--isolate is recommended for multi-cell organisms.
'''

rule spades:
	input:
		l = rules.trimmomatic.output.l_P,
		r = rules.trimmomatic.output.r_P
	output:
		assembly_folder = '../assemblies/{sample}_spades',
		contigs = '../assemblies/{sample}_spades/contigs.fasta'
	threads: 2
#	conda: '../../miniconda3/envs/snakemake'
	shell:	
		'spades.py -1 {input.l} -2 {input.r} --isolate -o {output.assembly_folder} -t {threads} --memory 100'


# MITOZ

'''
MitoZ provides a "one-click" solution to get annotated mitogenomes from raw data fastq files.
-findmitoscaf searches for mitochondrial sequences from input fasta file.
'''

rule mitoz:
	input:
		cont = rules.spades.output.contigs		
	output:
		mitoz_out = '{sample}_mitoscaf',
		result = '../mitoz_outputs/{sample}_mitoscaf.result/{sample}_mitoscaf.mitogenome.fa'
	threads: 2
	conda: 'mitoz'
	shell:
		'mitoz findmitoscaf --fastafile {input.cont} --clade Arthropoda --outprefix {output.mitoz_out}  --requiring_taxa Arthropoda --min_abundance 0 --thread_number {threads}; mv {output.mitoz_out}.result ../mitoz_outputs; rm {output.mitoz_out}*'


# FCS-GX
rule fcs:
	input:
		f = rules.spades.output.contigs
	output:
		report = '../gx_out/{sample}_fcsgx_out',
		contam = '../gx_out/{sample}_fcsgx_out/contam.fasta'
	threads: 2
#	conda: 'fcsgx'
	shell:
		'python3 ../fcs.py screen genome --fasta {input.f} --out-dir {output.report}/ --gx-db ../../../../../DATABIG/dbs/gxdb --tax-id 72766; '
		'cat {input.f} | python3 ../fcs.py clean genome --action-report {output.report}/contigs.72766.fcs_gx_report.txt --output {output.report}/clean.fasta --contam-fasta-out {output.contam}'


# MERGING CONTAMINANTS
rule merge:
	input:
		mito = rules.mitoz.output.result,
		contaminant = rules.fcs.output.contam
	output:
		ToRemove = '../ToRemove/{sample}_ToRemove.fasta'
	shell:
		'touch {output.ToRemove}; cat {input.mito} >> {output.ToRemove}; cat {input.contaminant} >> {output.ToRemove}'


# MINIMAP2
rule minimap:
	input:
		g  = rules.merge.output.ToRemove, 
		reads1 = rules.trimmomatic.output.l_P,
		reads2 = rules.trimmomatic.output.r_P
	output:
		bam = '../minimap_out/{sample}/{sample}_sr.bam',
	threads:
		10
	shell:
		'minimap2 -t {threads} -ax sr {input.g} {input.reads1} {input.reads2} | samtools view -Sb  > {output.bam}'	
# SAMTOOLS

rule samtools_sorted_bam:
	input:
		b = rules.minimap.output.bam
	output:
		sorted = '../minimap_out/{sample}/{sample}_sr.sorted.bam',
		index = '../minimap_out/{sample}/{sample}_sr.sorted.bam.bai'
	shell:
		'samtools sort -@10 -o {output.sorted} {input.b}; '
		'rm {input.b}; '
		'samtools index {output.sorted}'
		
rule samtools_fastq:
	input:
		s_bam = rules.samtools_sorted_bam.output.sorted
	output:
		fastq1 = '../minimap_out/{sample}/{sample}.clean_1.fastq',
		fastq2 = '../minimap_out/{sample}/{sample}.clean_2.fastq'
	shell:
		'samtools fastq -F 10 -@ 10 {input.s_bam} -1 {output.fastq1} -2 {output.fastq2}'

rule samtools_flagstat:
	input:
		fq1 = rules.samtools_fastq.output.fastq1,
		fq2 = rules.samtools_fastq.output.fastq2
	output:
		flag1 = '../minimap_out/{sample}/{sample}_sr1.flagstats',
		flag2 = '../minimap_out/{sample}/{sample}_sr2.flagstats'
	shell:
		'samtools flagstat {input.fq1} > {output.flag1}; '
		'samtools flagstat {input.fq2} > {output.flag2}'

